function(add_windows_icon TARGET_NAME ICON_PATH)
    if(WIN32)
        get_filename_component(ICON_EXT ${ICON_PATH} EXT)
        string(TOLOWER ${ICON_EXT} ICON_EXT_LOWER)

        set(ICO_PATH "")

        if(ICON_EXT_LOWER STREQUAL ".ico")
            if(EXISTS ${ICON_PATH})
                set(ICO_PATH ${ICON_PATH})
            endif()
        else()
            get_filename_component(ICON_DIR ${ICON_PATH} DIRECTORY)
            get_filename_component(ICON_NAME ${ICON_PATH} NAME_WE)
            set(ICO_PATH_CANDIDATE "${ICON_DIR}/${ICON_NAME}.ico")

            if(EXISTS ${ICO_PATH_CANDIDATE})
                set(ICO_PATH ${ICO_PATH_CANDIDATE})
            endif()
        endif()

        if(ICO_PATH)
            set(RC_FILE "${CMAKE_CURRENT_BINARY_DIR}/${TARGET_NAME}_icon.rc")
            file(WRITE ${RC_FILE} "IDI_ICON1 ICON DISCARDABLE \"${ICO_PATH}\"\n")
            target_sources(${TARGET_NAME} PRIVATE ${RC_FILE})
            message(STATUS "Added Windows icon to ${TARGET_NAME}: ${ICO_PATH}")
        else()
            message(STATUS "No .ico file found for exe icon (window icon will still work)")
        endif()
    endif()
endfunction()
